/* File:      flrtrim.P - trim the caller argument from Flora literals
**
** Author(s): Michael Kifer
**
** Contact:   kifer@cs.stonybrook.edu
**
** Copyright (C) by
**      The Research Foundation of the State University of New York, 1999-2010.
**
** All rights reserved.
**
** For information about licensing terms, please see
** http://silk.projects.semwebcentral.org/flora2-license.html
**
**
*/



%% Trims the Caller argument from Flora literals.
%% Used prior to inserting them into the database


:- compiler_options([xpp_on]).

#include "celltags_xsb.h"


:- import
	reverse/2,
	append/3,
	length/2
   from basics.
:- import
	term_psc/2,
	term_type/2,
	psc_arity/2,
	psc_name/2,
	term_arg/3
   from machine.
:- import flora_decode_module_prefix/2 from flrwrapper.

:- export
	flora_add_last/3,
	flora_bind_last/2,
	flora_replace_last/3,
	flora_generalize_last/2,
	flora_trim_last/2,
	flora_ground/1.

:- dynamic flora_trim_cache/2.
:- dynamic flora_generalization_cache/3.
:- dynamic flora_binding_cache/2.
:- dynamic flora_add_arg_cache/3.


%% Trims the last argument both from lists and predicates
%% Do not table this: performance suffers. Caching works fine.
flora_trim_last(X,X) :-
	var(X),
	!.
flora_trim_last(X,Y) :-
	flora_trim_cache(X,Y),
	!.
flora_trim_last(X,Y) :-
	is_list(X),
	!,
	length(X,InLen),
	get_arg_templates(trim,InLen,InArgsTempl,OutArgsTempl),
	asserta(flora_trim_cache(InArgsTempl,OutArgsTempl)),
	X=InArgsTempl,
	Y=OutArgsTempl.
flora_trim_last(X,Y) :-
	functor(X,Fun,InLen),
	get_arg_templates(trim,InLen,InArgsTempl,OutArgsTempl),
	InPredTempl =.. [Fun|InArgsTempl],
	OutPredTempl =.. [Fun|OutArgsTempl],
	asserta(flora_trim_cache(InPredTempl,OutPredTempl)),
	X=InPredTempl,
	Y=OutPredTempl.

%% get_arg_templates(+Optype,+Len,-ListTempl1,-ListTempl2)
%% Takes length, outputs a list template of the same length, ListTempl1
%% If Optype=generalize
%%    then ListTempl2 is ListTempl1 with last element changed to new variable
%% If Optype=trim
%%    then ListTempl2 is ListTempl1 with the last list element removed.
get_arg_templates(generalize,Len,ListTempl1,ListTempl3,LastArg) :-
	length(ListTempl1,Len),
	reverse(ListTempl1,ListTempl1reversed),
	ListTempl1reversed = [_|ListTempl2reversed],
	ListTempl3reversed = [LastArg|ListTempl2reversed],
	reverse(ListTempl3reversed,ListTempl3).
get_arg_templates(trim,Len,ListTempl1,ListTempl2) :-
	length(ListTempl1,Len),
	reverse(ListTempl1,ListTempl1reversed),
	ListTempl1reversed = [_|ListTempl2reversed],
	reverse(ListTempl2reversed,ListTempl2).

%%  ListTempl4 is ListTempl1 with one addl last list member, a  new var.
%%  get_arg_templates(add,+Len,-ListTempl1,-ListTempl4,-LastVar)
get_arg_templates(add,Len,ListTempl1,ListTempl4,LastVar) :-
	length(ListTempl1,Len),
	append(ListTempl1,[LastVar],ListTempl4).

%% Make a list of variables of length Len and bind the last variable to Binding
%% get_arg_templates(bind,+Len,-ListTempl1,+Binding)
get_arg_templates(bind,Len,ListTempl1,Binding) :-
	length(ListTempl1,Len),
	reverse(ListTempl1,ListTempl1reversed),
	ListTempl1reversed = [Last|_Rest],
	Last=Binding.

flora_generalize_last(X,Y) :-
	flora_generalize_last(X,Y,_).

flora_generalize_last(X,X,_LastArg) :-
	var(X),
	!.
flora_generalize_last(X,Y,LastArg) :-
	flora_generalization_cache(X,Y,LastArg),
	!.
flora_generalize_last(X,Y,LastArg) :-
	is_list(X),
	!,
	length(X,InLen),
	get_arg_templates(generalize,InLen,InArgsTempl,OutArgsTempl,LastArg),
	asserta(flora_generalization_cache(InArgsTempl,OutArgsTempl,LastArg)),
	X=InArgsTempl,
	Y=OutArgsTempl.
flora_generalize_last(X,Y,LastArg) :-
	functor(X,Fun,InLen),
	get_arg_templates(generalize,InLen,InArgsTempl,OutArgsTempl,LastArg),
	InPredTempl =.. [Fun|InArgsTempl],
	OutPredTempl =.. [Fun|OutArgsTempl],
	asserta(flora_generalization_cache(InPredTempl,OutPredTempl,LastArg)),
	X=InPredTempl,
	Y=OutPredTempl.

flora_add_last(X,X,X) :-
	var(X),
	!.
flora_add_last(X,Y,LastVar) :-
	%% if var(LastVar), it can pick up wrong stuff from cache
	nonvar(LastVar),
	flora_add_arg_cache(X,Y,LastVar),
	!.
flora_add_last(X,Y,LastVar) :-
	is_list(X),
	!,
	length(X,InLen),
	get_arg_templates(add,InLen,InArgsTempl,OutArgsTempl,LastVar),
	asserta(flora_add_arg_cache(InArgsTempl,OutArgsTempl,LastVar)),
	X=InArgsTempl,
	Y=OutArgsTempl.
flora_add_last(X,Y,LastVar) :-
	functor(X,Fun,InLen),
	get_arg_templates(add,InLen,InArgsTempl,OutArgsTempl,LastVar),
	InPredTempl =.. [Fun|InArgsTempl],
	OutPredTempl =.. [Fun|OutArgsTempl],
	asserta(flora_add_arg_cache(InPredTempl,OutPredTempl,LastVar)),
	X=InPredTempl,
	Y=OutPredTempl.

flora_bind_last(X,_) :-
	var(X),
	!.
flora_bind_last(X,Binding) :-
	flora_binding_cache(X,Binding),
	!.
flora_bind_last(X,Binding) :-
	is_list(X),
	!,
	length(X,InLen),
	get_arg_templates(bind,InLen,InArgsTempl,BindTempl),
	( flora_binding_cache(InArgsTempl,BindTempl), !
	; asserta(flora_binding_cache(InArgsTempl,BindTempl))
	),
	(X=InArgsTempl, BindTempl=Binding, !
	; true
	).
flora_bind_last(X,Binding) :-
	functor(X,Fun,InLen),
	get_arg_templates(bind,InLen,InArgsTempl,BindTempl),
	InPredTempl =.. [Fun|InArgsTempl],
	( flora_binding_cache(InPredTempl,BindTempl), !
	; asserta(flora_binding_cache(InPredTempl,BindTempl))
	),
	( X=InPredTempl, BindTempl=Binding, !
	; true
	).

%% flora_replace_last(+In,+ReplLastArg,-Out)
%% replaces the last arg of In with ReplLastArg
flora_replace_last(In,ReplLastArg,Out) :-
	flora_generalize_last(In,Out,ReplLastArg).


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% flora_ground/1 - like ground/1, but if the functor is a flora predicate %%
%%                  wrapper then do not check the last argument.           %%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

flora_ground(Y) :- term_type(Y,T),
	((T =:= XSB_FREE ; T =:= XSB_ATTV)
	->	fail
	; T =:= XSB_LIST
	->	Y=[A1|A2],
	 	flora_ground(A1), flora_ground(A2)
	 ; T =:= XSB_STRUCT
	 ->	term_psc(Y,PSC), psc_arity(PSC,A),
	        psc_name(PSC,Functor),
	        (flora_decode_module_prefix(Functor,_)
		-> %% flogic or HiLog predicate
		    AA is A - 1
		;   AA is A
		),
	 	flora_ground(Y,1,AA)
	 ;	true
	).
flora_ground(Y,N,A) :-
	(N > A
	-> true
	;
	    term_arg(Y,N,Arg),
	    flora_ground(Arg),
	    N1 is N+1,
	    flora_ground(Y,N1,A)
	).

