/* File:      flrdynmod.P
**
** Author(s): Guizhen Yang 
**
** Contact:   kifer@cs.stonybrook.edu
**
** Copyright (C) by
**      The Research Foundation of the State University of New York, 1999-2012;
**      and Vulcan, Inc., 2008-2012.
**
** All rights reserved.
**
** For information about licensing terms, please see
** http://silk.projects.semwebcentral.org/flora2-license.html
**
**
*/


:- compiler_options([xpp_on]).

#include "flora_terms.flh"
#include "flora_porting.flh"
#include "flora_exceptions.flh"


:- import append/3 from basics.

:- import
	flora_check_module_name/1,
	flora_module_predicate/4,
	flora_decode_predicate/7,
	is_flora_callable_literal/1
   from flrwrapper.

:- import
	flora_abort/1,
	flora_increment_counter/4
   from flrporting.

:- import 
        convert_to_body_literal/2,
        flora_check_dynmod_type_consistency/1,
        is_ws_distributive/3,
        is_caller_distributive/3,
        is_caller_distributive/4
    from flrcanon.

:- import FLLIBUNIVEQFORM/2 from usermod.

:- import
	flora_temp_protect_call/2,
	flora_temp_unprotect_call/1
    from flrundefined.

:- import
	flora_in_debug_mode/1,
	flora_debug_module_registry/1
   from flrregistry.

:- import
	flora_add_last/3,
	flora_replace_last/3,
	flora_bind_last/2
   from flrtrim.

:- import flora_add_undef_export_call/2,
	  flora_remove_undef_export_call/1
   from flrexport.

%% Tabling this INCREASES time, if FLLIBMODLIT/4 is also tabled
%%:- table get_libmod_callable/5.
%% Tabling this only increases time
%%:- table FLLIBMODLIT/4.
%% This table can save significant time when the number of metaops is large.
:- table FLLIBMODOBJ/5 as opaque.

/*********************************************************************
  This file is for the following dynamic module references:
  FLLIBMODLIT and FLLIBMODOBJ.

  FLLIBMODLIT is used when the compiler encounters literal@Variable
  where literal is F-logic or HiLog
  FLLIBMODOBJ is used when compiler finds term@Variable inside a predicate,
  e.g., f(g@V)
*********************************************************************/


/*********************************************************************
  FLLIBMODLIT(+Functor,+Args,+ModuleName,+CallerInfo)
  Functor is either an F-logic/HiLog wrapper or FL_LIBMOD or FL_NEGLIBMOD
*********************************************************************/
FLLIBMODLIT(F,Arg,ModuleName,CallerInfo) :-
	%% If module is a variable, we must protect the call
	%% from undefinedness checks
	(var(ModuleName) -> ProtectCall = true ; true),
	(
          var(Arg) ->
            (ModuleName==FL_INVALIDMODULE ->
	        flora_abort([Arg,': Attempt to call uninstantiated literal'])
            ;
	        flora_abort([Arg,'@',ModuleName,': Attempt to call uninstantiated literal'])
            )

        ; Arg=FL_TRANSACTIONAL(NA), var(NA) ->
            (ModuleName==FL_INVALIDMODULE ->
	        flora_abort([CH_PERCENT,NA,': Attempt to call uninstantiated literal'])
            ;
	        flora_abort([CH_PERCENT,NA,'@',ModuleName,': Attempt to call uninstantiated literal'])
            )

	; flora_check_module_name(ModuleName),
	  flora_check_dynmod_type_consistency(FLLIBMODLIT(F,Arg,ModuleName,CallerInfo)),
	  get_libmod_callable(F,Arg,ModuleName,CallerInfo,P),
	  %% Assign an Id for use by both the export & the undefinedness check.
	  (nonvar(ProtectCall) ->
	      %% New Id for protecting calls. Unique Id is used because
	      %% several calls to the same method/predicate may be in
	      %% progress at the same time, so we don't want to inadvertantly
	      %% unprotect a call that might still need to be protected
	      %% the same Id is also used for the export call.
	      flora_increment_counter(FLORA_SYMBOL('protect_undef_call'),1,_,IdExport),
	      flora_add_undef_export_call(P,IdExport),
	      %% Protect call P from undefinedness check
	      (  flora_in_debug_mode(_),
		  flora_debug_module_registry(ModuleName)
	      ->  IdUndef = IdExport,
		  flora_temp_protect_call(P,IdUndef)
	      ;   
		  IdUndef = 0
	      )
	  ;
	      IdExport = 0,
	      IdUndef = 0
	  ),
	  %% Whether P succeeds or fails we must remove the temporary
	  %% skeleton from the set of facts protected from undefinedness
	  %% checking in flrundefined.P so that undefinedness could be
	  %% checked again for P.
	  %% Likewise remove the predicate from the export undef registry.
	  %% It can happen that Id is not equal to 0 but IdUndef is.
	  (
	    call(P), 
	    (IdExport > 0 ->  flora_remove_undef_export_call(IdExport)
	    ;   true
	    ),
	    (IdUndef > 0 ->  flora_temp_unprotect_call(IdUndef)
	    ;   true
	    )
	  ; 
	    IdExport > 0, flora_remove_undef_export_call(IdExport), fail
	  ; 
	    IdUndef > 0, flora_temp_unprotect_call(IdUndef), fail
	  )
	).


/*********************************************************************
  FLLIBMODOBJ(+Functor,+Args,+ModuleName,+CallerInfo,-Predicate)
  This is used as follows: if we have a nested reification of a module'd
  literal, and either the module or the predicate/molecule part is a variable
  then FLLIBMODOBJ constructs the actual literal at run time.
  For instance, ?- p(?P@foo) is translated into
  ?- libmodobj(?P,foo,?Result), p(?Result).
*********************************************************************/
FLLIBMODOBJ(F,Arg,ModuleName,CallerInfo,Pout) :-
	var(Arg),
	Pout = FLLIBMODLIT(F,Arg,ModuleName,CallerInfo),
	!.
FLLIBMODOBJ(F,Arg,ModuleName,CallerInfo,Pout) :-
        flora_check_module_name(ModuleName),
	flora_check_dynmod_type_consistency(FLLIBMODLIT(F,Arg,ModuleName,CallerInfo)),
	%% Use a different var, NewCallerInfo, here. Not CallerInfo.
	%% We don't want to bind caller info in a reified term.
	get_libmod_callable(F,Arg,ModuleName,NewCallerInfo,P),
	(P =FLLIBMODLIT(F,_NArg,ModuleName,NewCallerInfo) ->
	    Pout = FLLIBMODLIT(F,Arg,ModuleName,NewCallerInfo)
	;
	    convert_to_body_literal(P,Pout)
        ).


/*********************************************************************
  get_libmod_callable(+Functor,+Args,+ModuleName,+CallerInfo,-Callable)

  This is common part of FLLIBMODLIT and FLLIBMODOBJ. Takes a term and
  a module and returns a callable structure (molecule or a predicate).
  The input can also be already callable. In this case just return in.
*********************************************************************/
get_libmod_callable(FL_LIBMOD,(FL_NEG_Atm(Arg)),ModuleName,CallerInfo,P) :-
	get_libmod_callable(FL_NEGLIBMOD,Arg,ModuleName,CallerInfo,P),
	!.
get_libmod_callable(FL_NEGLIBMOD,(FL_NEG_Atm(Arg)),ModuleName,CallerInfo,P) :-
	get_libmod_callable(FL_LIBMOD,Arg,ModuleName,CallerInfo,P),
	!.
get_libmod_callable(F,Arg,ModuleName,CallerInfo,P) :-
        (
	  is_list(Arg),
	  F == FL_LIBMOD,
	  ModuleName == FL_INVALIDMODULE,
	  !,
	  flora_abort([Arg,': Attempt to call a non-predicate'])
	; is_list(Arg), %% Term@?WS
	  !,
	  %% This case is compiled as FLLIBMODLIT(F,Args,WS,CallerInfo)
	  %% so the caller is already accounted for.
	  %% P is now made into a Flora wrapper
	  flora_module_predicate(F,Arg,ModuleName,P),
	  make_caller_info(CallerInfo,ModuleName,RealCallerInfo),
	  flora_bind_last(P, RealCallerInfo)
	; is_libmodlit_control(Arg, [_Wrap,_A,PredModule,_C]), !,
	  make_caller_info(CallerInfo,PredModule,RealCallerInfo),
	  flora_bind_last(Arg, RealCallerInfo),
	  P = Arg
	%% is_caller_distributive/3
        ; is_caller_distributive(Arg, _, _), !,
	  apply_caller_info(Arg,CallerInfo,P)
	%% is_caller_distributive/4
        ; is_caller_distributive(Arg, _, _, _), !,
	  apply_caller_info(Arg,CallerInfo,P)
	%% is_ws_distributive/3
        ; is_ws_distributive(Arg,Op,ArgList), !,
	  distribute_workspace(F,ArgList,ModuleName,CallerInfo,ArgListOut),
	  is_ws_distributive(P,Op,ArgListOut)
	; %% ?Var, ?Var@?M, or %?Var@..., Var bound to compound
	  %% F is FL_LIBMOD, FL_NEGLIBMOD, or FL_TRANSLIBMOD
	  flora_decode_predicate(Arg,_,PredModName,Wrapper,_,_,Args), !,
	  ( %% if already a hilog or flogic predicate -- use it
	      nonvar(PredModName),
	      !,
	      %% Arg is already a FLORA callable predicate,
	      %% so use *its* module name, NOT the one from CallerInfo
	      make_caller_info(CallerInfo,PredModName,RealCallerInfo),
	      %% Always replace the caller and replace whatever there is
	      %% with RealCallerInfo. Otherwise, general_tests/callReified.flr
	      %% won't work. The current caller info of X, where X is bound
	      %% to a reified formula may have been bound by a call from
	      %% another module and thus may be incorrect.
	      flora_replace_last(Arg,RealCallerInfo,P1),
	      (F == FL_NEGLIBMOD -> flrwrapper:negate_predicate(P1,P)
	      ; P = P1
	      )
	  ; ModuleName==FL_INVALIDMODULE,
	      !,
	      (F == FL_NEGLIBMOD -> flrwrapper:negate_predicate(Arg,P)
	      ; P = Arg
	      )
	  ; %% else, HiLog - construct it
	      (F == FL_LIBMOD -> WrapToUse = FLBodyPREFIX(WRAP_TABLED_HILOG)
	      ; F == FL_NEGLIBMOD -> WrapToUse = FLNEGBodyPREFIX(WRAP_TABLED_HILOG)
	      ; F == FL_TRANSLIBMOD -> WrapToUse = FLBodyPREFIX(WRAP_NONTABLED_HILOG)
	      ),
	      ( atom(Arg) ->
		  flora_add_last([Arg],AugmentedArgs,CallerInfo),
		  ( var(ModuleName) ->
		      P = FLLIBMODLIT(WrapToUse,AugmentedArgs,ModuleName,CallerInfo)
		  ;
		      flora_module_predicate(WrapToUse,AugmentedArgs,ModuleName,P)
		  )
	      ; Wrapper == WRAP_HILOG -> %% Note: should be WRAP_HILOG (as in HiLog term) here
		  flora_add_last(Args,AugmentedArgs,CallerInfo),
		  ( var(ModuleName) ->
		      P = FLLIBMODLIT(WrapToUse,AugmentedArgs,ModuleName,CallerInfo)
		  ;
		      flora_module_predicate(WrapToUse,AugmentedArgs,ModuleName,P)
		  )
	      ;
		  %% ?Var@..., neg ?Var@..., or %?Var@...
		  %% where Var bound to a prolog predicate
		  P = Arg
	      )
	  )
	),
	!.
	

%% Specifically for FLLIBMODLIT
is_libmodlit_control(FLLIBMODLIT(W,A,M,C), [W,A,M,C]).

distribute_workspace(_F,[],_Mod,_CallerInfo,[]) :- !.
distribute_workspace(F,[H|L],Mod,CallerInfo,[FLLIBMODLIT(F,H,Mod,CallerInfo)|NL]) :-
        distribute_workspace(F,L,Mod,CallerInfo,NL).

distribute_caller_info([],_CallerInfo,[]) :- !.
distribute_caller_info([H|OpArgList],CallerInfo,[Hout|OpArgListOut]) :-
	apply_caller_info(H,CallerInfo,Hout),
	distribute_caller_info(OpArgList,CallerInfo,OpArgListOut).

apply_caller_info(Goal,CallerInfo,GoalOut) :-
	is_ws_distributive(Goal,Op,GoalArgs),
	!,
	distribute_caller_info(GoalArgs,CallerInfo,ArgsOut),
	is_ws_distributive(GoalOut,Op,ArgsOut).
apply_caller_info(Goal,CallerInfo,GoalOut) :-
	is_caller_distributive(Goal,Op,GoalArgs,OtherArgs),
	!,
	distribute_caller_info(GoalArgs,CallerInfo,ArgsOut),
	is_caller_distributive(GoalOut,Op,ArgsOut,OtherArgs).
apply_caller_info(Goal,CallerInfo,GoalOut) :-
	is_caller_distributive(Goal,Op,GoalArgs),
	!,
	distribute_caller_info(GoalArgs,CallerInfo,ArgsOut),
	is_caller_distributive(GoalOut,Op,ArgsOut).
apply_caller_info(Goal,CallerInfo,GoalOut) :-
	(flora_decode_predicate(Goal,_,Module,_,_,_,_), nonvar(Module)
	->
	    make_caller_info(CallerInfo,Module,RealCallerInfo),
	    flora_bind_last(Goal,RealCallerInfo),
	    GoalOut = Goal
	; GoalOut = Goal
	).

%% make_caller_info(+DefaultCallerInfo,+RealModule,-RealCallerInfo)
%%make_caller_info([Caller,_| Rest],RealModule,[Caller,RealModule | Rest]).
make_caller_info([Caller,_],_RealModule,[Caller,INFONULL]).

