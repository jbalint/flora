/* File:        flrdebugger.P  -- Flora debugger
**
** Author(s): Michael Kifer
**
** Contact:   kifer@cs.stonybrook.edu
**
** Copyright (C) by
**      The Research Foundation of the State University of New York, 1999-2009.
**
** All rights reserved.
**
** For information about licensing terms, please see
** http://silk.projects.semwebcentral.org/flora2-license.html
**
**
*/



:- compiler_options([xpp_on]).
#include "standard.h"
#include "flag_defs_xsb.h"

#include "flrheader.flh"
#include "flora_terms.flh"



:- import
	debug_display_call_hook/1,
	debug_tracelog_predicate/1
   from usermod.
:- import flora_configuration/2 from flrregistry.
:- import process_control/2 from shell.
:- import xpp_process_file/3 from parse.
:- import file_flush/2 from file_io.

:- import flora_decode_predicate/6,
	flora_is_system_module_name/1,
	flora_module_predicate_symbol/3
    from flrwrapper.
:- import flora_set_xpp_options_for_read/1,
	flora_set_xpp_options_for_read/0,
	flora_clear_xpp_options/0 from flrutils.
:- import flora_loader_message/1, flora_loader_message/2 from flrprint.
:- import '_isloaded'/1 from flora2.

:- import flora_concat_atoms/2,
	flora_slash/1,
	flora_set_counter/2
   from flrporting.

:- import flora_write_goal/2 from flrdecode.

%% This is a trick: the debugger wants the hook to be in usermod, 
%% but we want to work in a module
:- import flora_write_debugger_hook/1 from usermod.

:- dynamic debug_display_call_hook/1.
:- dynamic flora_switch/1.
:- dynamic flora_write_debugger_hook/1.

:- dynamic
	hide_this_hide/2,
	show_this_hide/2,
	hide_this_show/2,
	trace_this_noprint/2,
	trace_this_print/2,
	flora_always_hide_this_hide/1,
	flora_always_hide_this_show/1,
	flora_hide_this_hide/1,
	flora_hide_this_show/1,
	flora_show_this_hide/1,
	flora_trace_this_noprint/1,
	flora_trace_this_print/1.
:- import 
	%%FLLIBCLAUSE/5,
	hide_this_hide/2,
	show_this_hide/2,
	hide_this_show/2,
	trace_this_noprint/2,
	trace_this_print/2,
	flora_hide_this_hide/1,
	flora_hide_this_show/1,
	flora_always_hide_this_hide/1,
	flora_always_hide_this_show/1,
	flora_show_this_hide/1,
	flora_trace_this_noprint/1,
	flora_trace_this_print/1
    from usermod.

:- ensure_loaded('flrclause.P').


:- import
	%%debug_ctl/2,
	make_trace_interactive/0,
	dump_trace_to_file/1,
	nodebug/0,
	set_debug_logging_queriable_trace/1,
	debug_logging_queriable_trace/1
   from debugger.

:- import
	stat_set_flag/2,
	stat_flag/2
   from machine.

:- export
	flora_switch/1,
	flora_read_debugger_data/1,
	flora_load_dyndata_for_user_modules/1,
	FLORA_SYMBOL('debugger_loaded'),
	flora_set_switch/1,
	flora_clear_switch/1,
	flora_handle_trace/0,
	flora_handle_notrace/0,
	flora_trace/0,
	flora_trace/1,
	flora_trace/2,
	flora_tracelow/0,
	flora_tracelow/1,
	flora_tracelow/2,
	flora_traceverylow/0,
	flora_traceverylow/1,
	flora_traceverylow/2,
	flora_notrace/0.


%% Assert the debugger hook
?- assert(debug_display_call_hook(flora_write_debugger_hook)),
   assert(debug_tracelog_predicate(flora_tracelog)).
?- assert((
           flora_write_debugger_hook(X) :-
	     (\+flora_switch(low_level_trace) -> flora_write_debug_call(X)
	     ; flora_write_debug_call_prolog(X)
	     )
	  )).


%% Get the debugging output stream
:- dynamic flora_dbg_stream/1.
?- (flora_dbg_stream(Stream) -> close(Stream), retractall(flora_dbg_stream(_))
   ; assert(flora_dbg_stream(STDDBG))
   ).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%% flora_write_debug_call %%%%%%%%%%%%%%%%%%%%%%%%%
%% write Call in Flora form + matching rules
flora_write_debug_call(Call) :-
	(debug_logging_queriable_trace(on) ->
	    flora_write_dbg_item('${'),
	    flora_write_debug_call_unreified(Call),
	    flora_write_dbg_item('}')
	/*
	   %% this writes the list of matching rules,
	   %% but it has too many variables and does not load
	    ,flora_write_dbg_item(', ['),
	    findall(':-'(Call,Body),
		    FLLIBCLAUSE(null,null,null,[Call],Body),
		    Rules),
	    write_matching_rules(Rules,flora)
	*/
	; flora_write_debug_call_unreified(Call)
	).


flora_write_debug_call_unreified(Call) :-
	flora_dbg_stream(Stream),
	flora_write_goal(Stream,Call).

%% write Call in prolog form + matching rules
flora_write_debug_call_prolog(Call) :-
	(debug_logging_queriable_trace(on)
	->
	    flora_writeq_dbg_item(Call)
	/*
	   %% this writes the list of matching rules,
	   %% but it has too many variables and does not load
	    ,flora_write_dbg_item(', ['),
	    findall(':-'(Call,Body),
		    FLLIBCLAUSE(null,null,null,[Call],Body),
		    Rules),
	    write_matching_rules(Rules,prolog)
	*/
	; flora_write_dbg_item(Call)
	).

flora_write_dbg_item(Item) :-
	flora_dbg_stream(Stream),
	write(Stream,Item).

flora_writeq_dbg_item(Item) :-
	flora_dbg_stream(Stream),
	writeq(Stream,Item).

flora_flush_dbg_stream :-
	flora_dbg_stream(Stream),
	file_flush(Stream, _).

/*
%% write_matching_rules(+RuleList,+ReifyFlag)
%% ReifyFlag=flora/prolog
write_matching_rules([],_) :-
	!,
	flora_write_dbg_item(']').
write_matching_rules([Rule|Rest],flora) :-
	flora_write_dbg_item('${'),
	write_rule(Rule,flora),
	flora_write_dbg_item('}'),
	(Rest==[], ! ; flora_write_dbg_item(', ')),
	write_matching_rules(Rest,flora).
write_matching_rules([Rule|Rest],prolog) :-
	flora_write_dbg_item('('),
	write_rule(Rule,prolog),
	flora_write_dbg_item(')'),
	(Rest==[], ! ; flora_write_dbg_item(', ')),
	write_matching_rules(Rest,prolog).

%% write_rule(Rule,Flag)
%% Flag: flora/prolog
write_rule((Head:-Body),flora) :-
        flora_write_debug_call_unreified(Head),
	flora_write_dbg_item(' :- '),
        flora_write_debug_call_unreified(Body).
write_rule((Head:-Body),prolog) :-
        flora_writeq_dbg_item(Head),
	flora_write_dbg_item(' :- '),
        flora_writeq_dbg_item(Body).
*/

/*********************************************************************/

flora_trace :-
	flora_set_switch(trace),
	%% this is needed to sidestep an XSB bug whereby 
	%% flrdebugger:flora_notrace is not executed right under the trace.
	%% Bug seems to manifest itself only under _trace, not _tracelow.
	(import flora_notrace/0 from flrdebugger),
	flora_handle_trace.
flora_trace(File) :-
	dump_trace_to_file(File),
	flora_trace.
flora_trace(File,log) :-
	set_debug_logging_queriable_trace(on),
	flora_trace(File).

%% Enables low-level Prolog debugging
flora_tracelow :-
	flora_trace,
	flora_set_switch(low_level_trace).
flora_tracelow(File) :-
	flora_trace(File),
	flora_set_switch(low_level_trace).
flora_tracelow(File,log) :-
	flora_trace(File,log),
	flora_set_switch(low_level_trace).
flora_traceverylow :-
	flora_trace,
	flora_set_switch(low_level_trace),
	flora_set_switch(verylow_level_trace).
flora_traceverylow(File) :-
	flora_trace(File),
	flora_set_switch(low_level_trace),
	flora_set_switch(verylow_level_trace).
flora_traceverylow(File,log) :-
	flora_trace(File,log),
	flora_set_switch(low_level_trace),
	flora_set_switch(verylow_level_trace).

flora_notrace :-
	flora_handle_notrace,
	set_debug_logging_queriable_trace(off),
	flora_flush_dbg_stream,
	make_trace_interactive,
	flora_clear_switch(trace),
	flora_clear_switch(verylow_level_trace),
	flora_clear_switch(low_level_trace).

/*
%% these are now in XSB
dump_trace_to_file(File) :-
	debug_ctl(prompt,off),
	debug_ctl(profile,on),
	debug_ctl(redirect,File).
make_trace_interactive :-
	debug_ctl(prompt,on),
	debug_ctl(profile,off),
	debug_ctl(redirect,tty).
*/

flora_handle_trace :-
	( flora_switch(trace)
	->  (stat_flag(TRACE,Trace), Trace == 0 -> trace
	    ; true
	    )
	; true
	).
flora_handle_notrace :-
	( stat_flag(TRACE,Trace), Trace > 0
	-> %% like debugger:notrace, but does not make tracing interactive
	    stat_set_flag(TRACE,0), % trace = 0
	    flora_flush_dbg_stream,
	    nodebug	
	; true
	).
	

/*********************************************************************/
%% This is needed so we could designate this predicate as unprintable 
%% by the debugger and avoid traicing inside 
%% assert(flora_switch(low_level_trace))
flora_set_switch(Switch) :-
	assert(flora_switch(Switch)).
flora_clear_switch(Switch) :-
	retractall(flora_switch(Switch)).



/*********************************************************************/

flora_debug_dyndata_file(Filename) :-
	flora_configuration(installdir,FlrSysLib),
	flora_slash(S),
	flora_concat_atoms([FlrSysLib,S,debugger,S,'dynamic_data.dat'],Filename).
flora_debug_statdata_file(Filename) :-
	flora_configuration(installdir,FlrSysLib),
	flora_slash(S),
	flora_concat_atoms([FlrSysLib,S,debugger,S,'static_data.dat'],Filename).

%% If WS is a var -- loading static data
%% Otherwise, loading dynamic data for the specified module
flora_read_debugger_data(WS,_Datafile) :-
	%% Check if already loaded
	(var(WS) 
	-> current_predicate(FLORA_SYMBOL('debugger_static_data_loaded')/0)
	; flora_module_predicate_symbol(debugger_data_loaded, WS, WSSym),
	    current_predicate(WSSym/0)
	),
	!.
flora_read_debugger_data(WS,Datafile) :- 
	(var(WS)
	-> flora_set_xpp_options_for_read
	; flora_set_xpp_options_for_read(WS)
	),
	xpp_process_file(Datafile, CPP_process, IOportFromCPP),
	repeat,
	    read(IOportFromCPP,T),
	    (T = end_of_file -> true
	    ;  assert(T), fail
	    ),
	process_control(CPP_process, wait(ExitStatus)),
	(ExitStatus==0, !
	; abort(('[Debugger] Error while preprocessing ', Datafile))
	),
	flora_clear_xpp_options,
	close(IOportFromCPP),
	flora_set_counter(xpp_on,0),
	(var(WS)
	->  flora_loader_message('Static debugger data loaded')
	;   flora_loader_message('Dynamic debugger data loaded for module ~w', [WS])
	),
	!.


%% Read dynamic data for preloaded user modules
flora_load_dyndata_for_user_modules([]).
flora_load_dyndata_for_user_modules([M|List]) :-
	flora_debug_dyndata_file(DataFile),
	flora_read_debugger_data(M,DataFile),
	flora_load_dyndata_for_user_modules(List).

flora_load_statdata :-
	flora_debug_statdata_file(DataFile),
	flora_read_debugger_data(_M,DataFile).

%% The bridge is disabled during low level tracing
flora_build_bridge_to_xsb_debugger :-
	assert((hide_this_hide(X,Y) :-
	       \+flora_switch(low_level_trace),flora_hide_this_hide(X/Y))),
	assert((hide_this_show(X,Y) :-
	       \+flora_switch(low_level_trace),flora_hide_this_show(X/Y))),
	assert((show_this_hide(X,Y) :-
	       \+flora_switch(low_level_trace),flora_show_this_hide(X/Y))),
	assert((trace_this_noprint(X,Y) :-
	       \+flora_switch(low_level_trace),flora_trace_this_noprint(X/Y))),
	assert((trace_this_print(X,Y) :-
	       \+flora_switch(low_level_trace),flora_trace_this_print(X/Y))),
	%% Rule for handling system modules.
	%% This prevents tracing inside flora system modules.
	%% We might add a switch that would enable this optionally.
	%% Not clear if this is useful.
	assert((show_this_hide(X,Y) :-
	       \+flora_switch(low_level_trace),
		functor(PX,X,Y),
		flora_decode_predicate(PX,_,WS,_,_,_),
		flora_is_system_module_name(WS)
	       )),
	%% Rules for prefixes of predicates that need 
	%% to be handled by the debugger
	assert((hide_this_hide(X,Y) :-
	       \+flora_switch(low_level_trace),
		(flora_hide_this_hide(pref(X1/Y)), atom(X),atom(X1), str_sub(X1,X,0), !
		 ; flora_hide_this_hide(match(X1/Y)), atom(X),atom(X1), str_sub(X1,X), !
		)
	       )),
	assert((hide_this_show(X,Y) :-
	       \+flora_switch(low_level_trace),
		( flora_hide_this_show(pref(X1/Y)), atom(X),atom(X1), str_sub(X1,X,0), !
		 ; flora_hide_this_show(match(X1/Y)), atom(X),atom(X1), str_sub(X1,X), !
		 ; flora_hide_this_show(propermatch(X1/Y)), atom(X),atom(X1), str_sub(X1,X), X1 \= X, !
		)
	       )),
	assert((show_this_hide(X,Y) :-
	       \+flora_switch(low_level_trace),
		( flora_show_this_hide(pref(X1/Y)), atom(X),atom(X1), str_sub(X1,X,0), !
		 ; flora_show_this_hide(match(X1/Y)), atom(X),atom(X1), str_sub(X1,X), !
                )
	       )),
	assert((trace_this_noprint(X,Y) :-
	       \+flora_switch(low_level_trace),
		( flora_trace_this_noprint(pref(X1/Y)), atom(X),atom(X1), str_sub(X1,X,0), !
		 ; flora_trace_this_noprint(match(X1/Y)), atom(X),atom(X1), str_sub(X1,X), !
		)
	       )),
	assert((trace_this_print(X,Y) :-
	       \+flora_switch(low_level_trace),
		( flora_trace_this_print(pref(X1/Y)), atom(X),atom(X1), str_sub(X1,X,0), !
		 ; flora_trace_this_print(match(X1/Y)), atom(X),atom(X1), str_sub(X1,X), !
		)
	       )),
	%% Stuff to always hide
	assert((hide_this_hide(X,Y) :-
	        \+flora_switch(verylow_level_trace),
	        flora_always_hide_this_hide(X/Y))),
	assert((hide_this_hide(X,Y) :-
	        \+flora_switch(verylow_level_trace),
		(flora_always_hide_this_hide(pref(X1/Y)), atom(X),atom(X1), str_sub(X1,X,0), !
		 ; flora_always_hide_this_hide(match(X1/Y)), atom(X),atom(X1), str_sub(X1,X), !
		)
	       )),
	assert((hide_this_show(X,Y) :-
	        \+flora_switch(verylow_level_trace),
		(flora_always_hide_this_show(pref(X1/Y)), atom(X),atom(X1), str_sub(X1,X,0), !
		 ; flora_always_hide_this_show(match(X1/Y)), atom(X),atom(X1), str_sub(X1,X), !
		)
	       )).

?- findall(X,'_isloaded'(X),L),
   flora_load_statdata,
   flora_load_dyndata_for_user_modules(L).

?- flora_build_bridge_to_xsb_debugger.

FLORA_SYMBOL('debugger_loaded').

